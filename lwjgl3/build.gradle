apply plugin: 'application'

sourceSets.main.resources.srcDirs += [ project.file('assets').path ]
mainClassName = 'squidpony.epigon.lwjgl3.Lwjgl3Launcher'
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 19

dependencies {
	api project(':core')
	api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	testImplementation 'com.github.tommyettinger:merry-ds:dc9e1bceb6'
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = project.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}
jar {
	// Sets the file name of the output JAR, to be placed in lwjgl3/build/libs/
	archiveFileName = "${appName}.jar"
	// Needed starting with Gradle 7.0.
	// This makes any repeated files included by default, though we exclude the ones we don't need later.
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	// Adds all of the files we built to the JAR.
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	// Gets rid of some files that are duplicated inside the JAR, and just act strangely. They aren't needed.
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
	// Sets the main class, to be run when the jar is run.
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archiveFile).setExecutable(true, false)
	}

}
